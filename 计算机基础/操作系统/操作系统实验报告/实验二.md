### 1 实验目的与要求

* 熟悉 hit-oslab 实验环境；
* 建立对操作系统引导过程的深入认识；
* 掌握操作系统的基本开发过程；
* 能对操作系统代码进行简单的控制，揭开操作系统的神秘面纱。

### 2 实验环境（软、硬件平台）

蓝桥平台lanqiao.cn

**主要硬件环境**

* IA-32（x86） 架构的 PC 机

**主要软件环境**

* Bochs
* Gcc
* Vim
* Ubuntu 16.04
* Linux 0.11 源代码

### 3 实验内容及步骤

**实验内容：**

*  阅读《Linux 内核完全注释》的第 6 章，对计算机和 Linux 0.11 的引导过程进行初步的了解；
*  按照要求改写 0.11 的引导程序 bootsect.s。
*  有兴趣同学可以做做进入保护模式前的设置程序 setup.s。

**实验步骤：**

  改写 `bootsect.s` 主要完成如下功能：

1. bootsect.s 能在屏幕上打印一段提示信息“XXX is booting...”，其中 XXX 是你给自己的操作系统起的名字，例如 LZJos、Sunix 等（可以上论坛上秀秀谁的 OS 名字最帅，也可以显示一个特色 logo，以表示自己操作系统的与众不同。）

改写 `setup.s` 主要完成如下功能：

1. bootsect.s 能完成 setup.s 的载入，并跳转到 setup.s 开始地址执行。而 setup.s 向屏幕输出一行"Now we are in SETUP"。
2. setup.s 能获取至少一个基本的硬件参数（如内存参数、显卡参数、硬盘参数等），将其存放在内存的特定地址，并输出到屏幕上。
3. setup.s 不再加载 Linux 内核，保持上述信息显示在屏幕上即可。

### 4 实验过程记录

#### 0x01. 备份原版bootsect.s和setup.s

进入`linux-0.11/boot`文件夹，备份`bootsect.s`和`setup.s`

```bash
cd ~/oslab/linux-0.11/boot

mv bootsect.s bootsect.s.backup && mv setup.s setup.s.backup
```

![image-20200526120535618](%E5%AE%9E%E9%AA%8C%E4%BA%8C.assets/image-20200526120535618.png)



#### 0x02. 屏幕打印自己logo

1. 用Vim创建`bootsect.s`文件，添加如下代码

```bash
 vim bootsect.s
```

```bash
entry _start
_start:
    mov ah,#0x03
    xor bh,bh
    int 0x10
    mov cx,#36
    mov bx,#0x0007
    mov bp,#msg1
    mov ax,#0x07c0
    mov es,ax
    mov ax,#0x1301
    int 0x10
inf_loop:
    jmp inf_loop
msg1:
    .byte   13,10
    .ascii  "Hello, LongWeiOS in loading..."
    .byte   13,10,13,10
.org 510
boot_flag:
    .word   0xAA55
```

2. 添加完成之后，保存退出，使用如下命令编译和链接`bootsect.s`。 如果没有报错表示编译和链接通过。

```bash
$ as86 -0 -a -o bootsect.o bootsect.s
$ ld86 -0 -s -o bootsect bootsect.o
```

![image-20200526125826872](%E5%AE%9E%E9%AA%8C%E4%BA%8C.assets/image-20200526125826872.png)

 

3. 去掉`bootsect.s`文件的头部，以便保证只需要一个引导区就可以保存`bootsect.s`引导文件

```bash
$ dd bs=1 if=bootsect of=Image skip=32
```

4. 将刚刚生成的 Image 复制到 linux-0.11 目录下，然后执行 `oslab` 目录中的 run 脚本

```bash
cp Image ../Image && ../../run
```

![image-20200526130439642](%E5%AE%9E%E9%AA%8C%E4%BA%8C.assets/image-20200526130439642.png)



#### 0x03. bootsect.s 读入 setup.s

1. 新建一个终端，进入到 `/linux-0.11/boot` 目录下，用Vim 创建 `setup.s`文件，添加如下代码

```bash
cd ~/oslab/linux-0.11/boot

vim setup.s
```

```bash
entry _start
_start:
    mov ah,#0x03
    xor bh,bh
    int 0x10
    mov cx,#25
    mov bx,#0x0007
    mov bp,#msg2
    mov ax,cs
    mov es,ax
    mov ax,#0x1301
    int 0x10
inf_loop:
    jmp inf_loop
msg2:
    .byte   13,10
    .ascii  "Now we are in setup"
    .byte   13,10,13,10
.org 510
boot_flag:
    .word   0xAA55
```



2. 使用Vim修改`bootsect.s`的代码，修改后的完整代码如下所示

```bash
vim bootsect.s
```

```bash
SETUPLEN=2
SETUPSEG=0x07e0
entry _start
_start:
    mov ah,#0x03
    xor bh,bh
    int 0x10
    mov cx,#36
    mov bx,#0x0007
    mov bp,#msg1
    mov ax,#0x07c0
    mov es,ax
    mov ax,#0x1301
    int 0x10
load_setup:
    mov dx,#0x0000
    mov cx,#0x0002
    mov bx,#0x0200
    mov ax,#0x0200+SETUPLEN
    int 0x13
    jnc ok_load_setup
    mov dx,#0x0000
    mov ax,#0x0000
    int 0x13
    jmp load_setup
ok_load_setup:
    jmpi    0,SETUPSEG
msg1:
    .byte   13,10
    .ascii  "Hello, LongWeiOS in loading..."
    .byte   13,10,13,10
.org 510
boot_flag:
    .word   0xAA55
```

3. 注释`linux-0.11/tools/build.c`里最后几行代码，防止出错

```bash
cd ~/oslab/linux-0.11/tools/ && vim build.c
```

![image-20200526133313866](%E5%AE%9E%E9%AA%8C%E4%BA%8C.assets/image-20200526133313866.png)



4. 编译运行

```bash
cd ~/oslab/linux-0.11

make BootImage

../run
```

![image-20200526133549716](%E5%AE%9E%E9%AA%8C%E4%BA%8C.assets/image-20200526133549716.png)





#### 0x04. setup.s 获取硬件参数并打印

1. 打开一个新终端，进入到`~/oslab/linux-0.11/boot`， 使用Vim 修改`setup.s`， 完整代码如下

```bash
cd ~/oslab/linux-0.11/boot && vim setup.s
```

```bash
INITSEG  = 0x9000
entry _start
_start:
! Print "Now we are in setup"
    mov ah,#0x03
    xor bh,bh
    int 0x10
    mov cx,#25
    mov bx,#0x0007
    mov bp,#msg2
    mov ax,cs
    mov es,ax
    mov ax,#0x1301
    int 0x10

    mov ax,cs
    mov es,ax
! init ss:sp
    mov ax,#INITSEG
    mov ss,ax
    mov sp,#0xFF00

! Get Params
    mov ax,#INITSEG
    mov ds,ax
    mov ah,#0x03
    xor bh,bh
    int 0x10
    mov [0],dx
    mov ah,#0x88
    int 0x15
    mov [2],ax
    mov ax,#0x0000
    mov ds,ax
    lds si,[4*0x41]
    mov ax,#INITSEG
    mov es,ax
    mov di,#0x0004
    mov cx,#0x10
    rep
    movsb

! Be Ready to Print
    mov ax,cs
    mov es,ax
    mov ax,#INITSEG
    mov ds,ax

! Cursor Position
    mov ah,#0x03
    xor bh,bh
    int 0x10
    mov cx,#18
    mov bx,#0x0007
    mov bp,#msg_cursor
    mov ax,#0x1301
    int 0x10
    mov dx,[0]
    call    print_hex
! Memory Size
    mov ah,#0x03
    xor bh,bh
    int 0x10
    mov cx,#14
    mov bx,#0x0007
    mov bp,#msg_memory
    mov ax,#0x1301
    int 0x10
    mov dx,[2]
    call    print_hex
! Add KB
    mov ah,#0x03
    xor bh,bh
    int 0x10
    mov cx,#2
    mov bx,#0x0007
    mov bp,#msg_kb
    mov ax,#0x1301
    int 0x10
! Cyles
    mov ah,#0x03
    xor bh,bh
    int 0x10
    mov cx,#7
    mov bx,#0x0007
    mov bp,#msg_cyles
    mov ax,#0x1301
    int 0x10
    mov dx,[4]
    call    print_hex
! Heads
    mov ah,#0x03
    xor bh,bh
    int 0x10
    mov cx,#8
    mov bx,#0x0007
    mov bp,#msg_heads
    mov ax,#0x1301
    int 0x10
    mov dx,[6]
    call    print_hex
! Secotrs
    mov ah,#0x03
    xor bh,bh
    int 0x10
    mov cx,#10
    mov bx,#0x0007
    mov bp,#msg_sectors
    mov ax,#0x1301
    int 0x10
    mov dx,[12]
    call    print_hex

inf_loop:
    jmp inf_loop

print_hex:
    mov    cx,#4
print_digit:
    rol    dx,#4
    mov    ax,#0xe0f
    and    al,dl
    add    al,#0x30
    cmp    al,#0x3a
    jl     outp
    add    al,#0x07
outp:
    int    0x10
    loop   print_digit
    ret
print_nl:
    mov    ax,#0xe0d     ! CR
    int    0x10
    mov    al,#0xa     ! LF
    int    0x10
    ret

msg2:
    .byte 13,10
    .ascii "Now we are in setup"
    .byte 13,10,13,10
msg_cursor:
    .byte 13,10
    .ascii "Cursor position:"
msg_memory:
    .byte 13,10
    .ascii "Memory Size:"
msg_cyles:
    .byte 13,10
    .ascii "Cyls:"
msg_heads:
    .byte 13,10
    .ascii "Heads:"
msg_sectors:
    .byte 13,10
    .ascii "Sectors:"
msg_kb:
    .ascii "KB"

.org 510
boot_flag:
    .word 0xAA55
```



2. 编译运行

```bash
cd ~/oslab/linux-0.11

make BootImage

../run
```

![image-20200526143246939](%E5%AE%9E%E9%AA%8C%E4%BA%8C.assets/image-20200526143246939.png)



### 5 实验数据分析及小结

**Question**

有时，继承传统意味着别手蹩脚。x86 计算机为了向下兼容，导致启动过程比较复杂。请找出 x86 计算机启动过程中，被硬件强制，软件必须遵守的两个“多此一举”的步骤（多找几个也无妨），说说它们为什么多此一举，并设计更简洁的替代方案。

**Answer 1**

我认为操作系统完全没必要非要二级引导。二级引导的只要原因我认为就是起初操作系统引导程序比较小，所以在设计BIOS时，一次只将512字节载入内存。我认为可以重新设计一下BIOS，使BIOS可以将更多扇区的内容一次载入内存，使引导程序全部载入内存中，这样一级引导就可以载入操作系统内核，提高了操作系统的启动速度

**Answer 2**

利用A20地址线以兼容传统也是多此一举，早期的8086只有20根地址线，只能访问1M的地址空间。CPU寻址则按段+偏移的方式进行。但现在，虽然系统的地址总线由原来的20根发展为24根，这样能够访问的内存可以达到2^24=16M,但是Intel在设计80286时提出的目标是向下兼容, 所以在实模式下，系统所表现的行为应该和8086/8088所表现的完全一样，也就是说，在实模式下，80386以及后续系列应该和8086/8088完全兼容仍然使用A20地址线。
       解决方案：是不向下兼容直接进入32位的保护模式，经管在Intel 80286手册中已经提出了虚地址保护模式，但实际上它只是一个指引，真正的32位地址出现在Intel 80386上。